#!/usr/bin/env zsh

include 'editor'
include 'keys'

function mkcd () { mkdir -p "$1" && cd "$1" }

setopt auto_cd
alias -- -='cd -'

if ! zgen saved; then
    zgen load Tarrasch/zsh-bd
fi


function {
    fasd_init="$ZGEN_DIR/init-fasd.zsh"
    if ! zgen saved; then
        echo Creating "$fasd_init"
        fasd --init zsh-hook posix-alias zsh-ccomp zsh-ccomp-install zsh-wcomp zsh-wcomp-install > "$fasd_init"
    fi
    source "$fasd_init"
}

alias j=z
jj() {
    local dir
    dir="$(fasd -Rdl | fzf -1 -0 --no-sort +m -q "$1")" && cd "${dir}" || return 1
}

# cd into recent dir on tab at beginning of line
function _cd_into_frecent_or_expand() {
    if [[ -z $BUFFER ]]; then
        BUFFER="jj"
        zle accept-line
    else
        zle expand-or-complete
    fi
}
zle -N _cd_into_frecent_or_expand
bindkey "$keys[Tab]" _cd_into_frecent_or_expand

